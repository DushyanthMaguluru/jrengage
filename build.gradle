// mvn install:install-file -Dfile=jrengage-release.aar -DgroupId=me.dm7.jrengage.android -DartifactId=jrengage -Dversion=3.7 -Dpackaging=aar

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.google.com' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername , password: sonatypePassword)
            releases(updatePolicy: 'always')
        }

        snapshotRepository(url: sonatypeSnapshotRepo) {
            authentication(userName: sonatypeUsername , password: sonatypePassword)
            snapshots(updatePolicy: 'always')
        }

        pom.project {
            name 'jrengage'
            version '3.8'
            groupId 'me.dm7.jrengage.android'
            packaging 'aar'
            artifactId 'jrengage'
            description "This is just a clone of the official janrain repo with only addition being the build.gradle file."
            url 'https://github.com/dm77/jrengage'

            scm {
                url 'scm:git@github.com:dm77/jrengage.git'
                connection 'scm:git@github.com:dm77/jrengage.git'
                developerConnection 'scm:git@github.com:dm77/jrengage.git'
            }

            licenses {
                license {
                    name 'Berkeley License'
                    url 'https://github.com/janrain/engage.android/blob/master/LICENSE'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'dm77'
                    name 'Dushyanth Maguluru'
                    email 'dushyanth.maguluru@gmail.com'
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/groups/public'
    }
    google()
}

dependencies {
    compile 'com.android.support:support-v4:21.0.3'
}

android {
    compileSdkVersion 21
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 38
        versionName '3.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}
